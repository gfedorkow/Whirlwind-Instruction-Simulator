; | [Transcipt by L. Stabile, April 8, 2025, from Subroutines.pdf, pdf-pages 22-24.
; |
; | Text in square brackets are transcription comments.  As of xxx this
; | file should be considered read-only, having been superseded by its
; | generated .ww counterpart.]
; |
; |                         DIGITAL COMPUTER LABORATORY
; |                   MASSACHUSETTS INSTITUTE OF TECHNOLOGY
; |                    WHIRLWIND SUBROUTINE SPECIFICATION
; |   
; |   TITLE: (30,0,0) MRA Print and/or Punch, Decimal Fraction,               LSR Of 103.10t
; |          Sign, Number of Digits Arbitrary, No Carriage                    TAPE T799-1
; |          Return, Sign Agreement (Interpreted)                             Classification
; |                                                                          Closed, Interpreted
; |                                                                           
; |   No. of Regs.        Temp. Regs.        Average Time                     Max. Time
; |   in Subroutine       used by Sub.       (operations)                    (operations)
; |        76                  4          94+19n, number of digits            94 + 27n
; |                                              printed = n
; |   
; |   Preset Parameters
; |   
; |       vx; pN, where N is the address in storage of the first register of the
; |               interpretive subroutine (in title of main program)
; |       vl: pO (does not need to be inserted) to print, p64 to punch, or pl28
; |               to print and punch simultaneously
; |       v2: pn, where n is the number of decimal digits to be printed.
; |   
; |   Description
; |   
; |       This subroutine prints and/or punches the sign and magnitude of the
; |   contents of the MRA in the following manner
; |   
; |            +-.d1d2...dn
; |   
; |   The number, n, of decimal digits to be printed is a preset parameter (v2). The
; |   digits, di, are obtained by multiplying the magnitude of the contents of the MRA
; |   successively by p10.
; |   
; |   This subroutine contains a sign agreement program so that the contents
; |   of the MRA need not be a number whose major and minor parts are of like sign.
; |   
; |   The sp instruction transferring control to this subroutine must be an
; |   interpreted sp (i.e., control must be in the interpretive subroutine). After
; |   execution of the subroutine control remains in the interpretive subroutine which
; |   then proceeds to interpret the instruction following the sp instruction in
; |   storage.
; |   
; |   There is no carriage return.
; |   
; |   This subroutine can be used with any(30,0,0) interpretive subroutine.
; |   The contents of the MRA are left undisturbed during the execution of this
; |   subroutine.
; |   
; |    FCH      MRS
; |   2/1/52  2/8/52
; |   
; |   Abstract: This subroutine prints out a +- sign and a decimal point followed
; |             by the magnitude of the contents of the MRA as a decimal fraction.
; |             The decimal digits are obtained by multiplying the contents of the
; |             MRA successively by plO. The number of digits to be printed is
; |             a preset parameter (v2). There is no carriage return. The sub-
; |             routine is interpreted and can be used with any (30,0,0) inter-â€”
; |             pretive subroutine.
; |
; |   Temporary Storage
; |       d       unused
; |       1t   -|
; |       2t    |- Temporary storage
; |       3G   -|
; |       4t      Digit counter
; |
; |
fp:
          ta 37fp
          sp ax                ; Resume ordinary ww operation
          ca 3ax               ; -|
          ts 2t                ;  |- Store C(mra) in 1t and 2t
          ca 2ax               ;  |
5fp:      ts 1t                ; -|
6fp:      mr 2t                ; Do 1t and 2t agree in sign?
          cp 38fp
          ca 1t                ; -|
9fp:      cp 12fp              ;  |
          ca 74fp              ;  |
          sp 13fp              ;  |- Sense and print algebraic sign of C(mra) followed by a decimal point
12fp:     ca 75fp              ;  |
13fp:     qp 134sl             ;  |
          qp 128sl             ; -|

          ca 61fp              ; -|- Set up digit counter
          ts 4t                ; -|

17fp:     cm 1t                ; -|
          mh 62fp              ;  |
          ts 3t                ;  |
          slr 15               ;  |
          ts 1t                ;  |- Multiply C(1t,2t) by p10
          cm 2t                ;  |
          mh 62fp              ;  |
          ts 2t                ;  |
          slr 15               ;  |
          ex 2t                ;  |
          sa 1t                ;  |
          ts 1t                ; -|

          ca 3t                ; -|
          ad 63fp              ;  |- Set up entry into table
          td 32fp              ; -|

32fp:     ca 0                 ; -|- Print a single digit
          qp 128sl             ; -|

          ao 4t                ; -|- Have enough digits been printed?
          cp 17fp              ; -|

          sp ax                ; -|- Return control to int. subroutine
37fp:     sp 0                 ; -|

38fp:     cm 1t                ; -|
          su 0                 ;  |- Is C(1t) != 0?
          cp 57fp              ; -|

          cm 2t                ; -|
          su 0                 ;  |- Is C(2t) != 0?
          cp 51fp              ; -|

          su 60fp              ; -|
          ad 59fp              ;  |- Form 1 - |C(2t)|
          ts 2t                ; -|

          ca 1t                ; -|- Is C(1t) pos.?
          cp 55fp              ; -|

          su 60fp              ; -|- Form C(1t) - 2^-15
          ts 1t                ; -|

51fp:     cs 2t                ; -|- Complement C(2t)
          ts 2t                ; -|

          ca 1t                ; -|- Re-enter sign agreement
          sp 6fp               ; -|

55fp:     ao 1t
          sp 9fp
57fp:     cs 1t
          sp 5fp
59fp:     ca 0                 ; ; [59-73 are notated in Subroutines.pdf with no spaces, e.g., ca0, p1]
60fp:     .word 1
61fp:     .word 10             ; v2 ; [was just n1a2 -- need to track down this notation]
62fp:     .word 10
63fp:     .word 64fp           ; , ; [I think comma? Might just be a blemish]
64fp:     .word 45             ; 0 Table
          .word 36             ; 1
          .word 39             ; 2
          .word 3              ; 3
          .word 21             ; 4
          .word 33             ; 5
          .word 43             ; 6
          .word 15             ; 7
          .word 13             ; 8
          .word 49             ; 9




; ;; Local Variables:
; ;; eval: (setq-local default-tab-width 20)
; ;; eval: (setq-local tab-width 20)
; ;; eval: (setq-local tab-stop-list '(20 40 60))
; ;; End:
